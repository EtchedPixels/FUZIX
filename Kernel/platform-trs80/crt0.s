	        ; Ordering of segments for the linker.
	        ; WRS: Note we list all our segments here, even though
	        ; we don't use them all, because their ordering is set
	        ; when they are first seen.	
	        .area _CODE
	        .area _CODE2
		.area _VIDEO
	        .area _CONST
	        .area _DATA
	        .area _INITIALIZED
	        .area _BSEG
	        .area _BSS
	        .area _HEAP
	        ; note that areas below here may be overwritten by the heap at runtime, so
	        ; put initialisation stuff in here
	        .area _INITIALIZER
	        .area _GSINIT
	        .area _GSFINAL
	        .area _COMMONMEM
		.area _DISCARD

        	; imported symbols
        	.globl _fuzix_main
	        .globl init_early
	        .globl init_hardware
	        .globl s__DATA
	        .globl l__DATA
	        .globl s__DISCARD
	        .globl l__DISCARD
	        .globl s__COMMONMEM
	        .globl l__COMMONMEM
		.globl s__INITIALIZER
	        .globl kstack_top

	        ; startup code
	        .area _CODE

;
;	Once the loader completes it jumps here
;
start:
		ld sp, #kstack_top
		; move the common memory where it belongs    
		ld hl, #s__INITIALIZER
		ld de, #s__COMMONMEM
		ld bc, #l__COMMONMEM
		ldir
		ld de, #s__DISCARD
		ld bc, #l__DISCARD
		ldir
		; then zero the data area
		ld hl, #s__DATA
		ld de, #s__DATA + 1
		ld bc, #l__DATA - 1
		ld (hl), #0
		ldir

;	TODO: Move the common into the other bank, pain as we may well have
;	code in low bank and __COMMON packed in high. Needs to be in
;	.COMMONMEM and map the other page low
;
		call init_early
		call init_hardware
		call _fuzix_main
		di
stop:		halt
		jr stop




clear:
		ld a, b
		or a
		jr nz, clear_1
		ld a, c
		cp #2
		ret c
clear_1:
		dec bc
		ld (hl), #0
		ld d, h
		ld e, l
		inc de
		ldir
		ret
